buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url 'http://dl.bintray.com/jetbrains/intellij-plugin-service'
        }
    }
    dependencies {
        classpath "org.jetbrains.intellij.plugins:gradle-intellij-plugin:0.5.0-SNAPSHOT"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.21"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
}

apply plugin: 'org.jetbrains.intellij'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.reflections:reflections:0.9.11'
}

intellij {
    version 'IC-2019.1'
    plugins = ['gradle', 'org.jetbrains.kotlin:1.3.30-release-IJ2019.1-1']
    pluginName 'gradle-integration-testing-plugin'
    sandboxDirectory = "$project.rootDir/local"
}

tasks.getByName('runIde') {
    def workingDir = System.getenv("working_dir")
    if (workingDir == null) {
        workingDir = "../jps"
    }
    def testName = System.getenv("test_name")
    if (testName == null) {
        testName = 'JpsImportAndBuild'
    }
    def heapSize = System.getenv("idea_heap_size")
    if (heapSize == null) {
        heapSize = '2G'
    }
    def absoluteWorkingDir = new File(workingDir).getCanonicalFile().getAbsolutePath().toString()
    println("Executing test [$testName] in folder [$absoluteWorkingDir]")
    systemProperties = ['java.awt.headless': 'true', 'projectPath': absoluteWorkingDir, 'jdkPath': System.getenv('JDK_18')]
    args = ['runIntegrationTest', testName]
    maxHeapSize = heapSize
}

group 'org.jetbrains'
version '0.1'
